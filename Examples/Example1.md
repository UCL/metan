<a href ="https://www.mrcctu.ucl.ac.uk/"><img src="../logo_ukri-mrc-ctu_transparent-background.png" width="50%" /></a>

# Example 1
An "umbrella" meta-analysis, where multiple related pooled results are presented in the same forest plot

Data taken from Supplementary Table 3 of Neuenschwander et al BMJ 2019 doi: 10.1136/bmj.l2368

<a href ="https://github.com/UCL/metan/blob/main/Examples"><img src="Example%201.png" width="50%" /></a>

```Stata
* Example generated by -dataex-. For more info, type help dataex
clear
input str44 Exposure str18 Reference byte N_Studies str13 Comparison str35 Range_Amount long(N_Pts N_Cases) float(SHR SHR_LCI SHR_UCI pvalue) str5 I2 float(tau2 LPI UPI Egger) byte AMSTAR str8 NutriGrade
"Healthy dietary pattern"                      "Esposito 2014"      18 "High v low"    "Higher adherence vs Lower adherence" 446213 21566  .79  .74  .85    0 "54.1%" .0106  .62  .99 .87 8 "Moderate"
"Unhealthy dietary pattern"                    "Alhazmi 2014"       10 "High v low"    "Higher adherence vs Lower adherence" 291406 19149 1.44 1.33 1.56    0 "3.0%"  .0006 1.29 1.61 .26 9 "Moderate"
"Healthy Eating Index (HEI)"                   "Schwingshackl 2017"  3 "High v low"    "Higher adherence vs Lower adherence" 262184    .a  .86  .79  .93    0 "76.7%" .0037  .32 2.17 .89 9 "Low"     
"Alternative Healthy Eating Index (AHEI)"      "Schwingshackl 2017"  9 "High v low"    "Higher adherence vs Lower adherence" 564048    .a  .79  .73  .85    0 "78.3%" .0095  .62 1.01 .52 9 "Moderate"
"Dietary Approach to Stop Hypertension (DASH)" "Schwingshackl 2017"  7 "High v low"    "Higher adherence vs Lower adherence" 258893    .a   .8  .73  .88    0 "67.4%" .0072  .63 1.03 .66 9 "Low"     
"Mediterranean diet"                           "Koloverou 2014"      9 "High v low"    "Higher adherence vs Lower adherence" 135168 19609  .85  .76  .95 .004 "59.0%" .0131  .63 1.15 .66 9 "Low"     
"Vegetarian diet"                              "Lee & Park 2017"     2 "High v low"    "Vegetarian v Omnivores"               49788    .a  .67  .54  .84    0 "58.3%" .0158   .b   .b  .a 7 "Low"     
"Low carbohydrate diet"                        "Namazi 2017"         4 "High v low"    "Higher adherence vs Lower adherence" 130515  8081 1.17   .9 1.51  .25 "81.5%" .0478  .39 3.51 .82 8 "Very low"
"Breakfast skipping"                           "Bi 2015"             4 "High v low"    "Everyday v Never "                    81666  4197 1.21 1.12 1.31    0 "0.0%"      0 1.02 1.44  .1 8 "Low"     
"Glycemic Index"                               "Bhupathiraju 2014"  13 "High v low"    "NA"                                      .a 35715 1.13 1.03 1.24  .01 "77.8%" .0204  .81 1.58 .17 8 "Low"     
"Glycemic Load"                                "Bhupathiraju 2014"  17 "High vs low"   "NA"                                      .a 46115 1.11 1.05 1.17    0 "36.0%"  .004  .96 1.29 .15 8 "Moderate"
"Dietary acid load"                            "Jayedi 2018"         6 "Dose-response" "Per 5 units/d"                       319542 17983 1.04 1.02 1.07   .a "79.2%"    .a   .a   .a  .a 9 "Moderate"
end

gen eff = ln(SHR)
gen lci = ln(SHR_LCI)
gen uci = ln(SHR_UCI)

gen predlci = ln(LPI)
gen preduci = ln(UPI)

gen byte use = 3
forestplot eff lci uci, use(use) labels(Exposure) rfdist(predlci preduci) nowt leftjustify ///
	nodiam ppointopts(msize(small)) rfopts(lcolor(gs12) lwidth(vthick) overlay) ///
	legend(off)
	
clonevar Exposure2 = Exposure
replace Exposure2 = "HEI" if Exposure2=="Healthy Eating Index (HEI)"
replace Exposure2 = "AHEI" if Exposure2=="Alternative Healthy Eating Index (AHEI)"
replace Exposure2 = "DASH" if Exposure2=="Dietary Approach to Stop Hypertension (DASH)"
format %-1s Exposure2
label variable Exposure2 "Exposure"

clonevar Range_Amount2 = Range_Amount
replace Range_Amount2 = "Higher vs lower adherence" if Range_Amount2=="Higher adherence vs Lower adherence"
format %-1s Range_Amount2
label variable Range_Amount2 "Comparison"

gen N_Studies_Str = strofreal(N_Studies)
format %~7s N_Studies_Str
label variable N_Studies_Str "No. studies"

gen N_Cases_Str = strofreal(N_Cases)
replace N_Cases_Str = "NA" if N_Cases_Str==".a"
format %~5s N_Cases_Str
label variable N_Cases_Str "No. cases"

forestplot eff lci uci, use(use) labels(Exposure2) rfdist(predlci preduci) nowt eform effect(SHR) ///
	nodiam rfopts(lcolor(gs12) lwidth(vthick) overlay) plotid(NutriGrade, list) ///
	ppoint1opts(msize(small) mlcolor(green)) ppoint2opts(msize(small) mlcolor(orange)) ppoint3opts(msize(small) mlcolor(maroon)) ///
	pci1opts(lcolor(green)) pci2opts(lcolor(orange)) pci3opts(lcolor(maroon)) ///
	legend(order(8 "Moderate" 9 "Low" 10 "Very Low") rows(1) size(2) subtitle("NutriGrade meta-evidence score:", size(2)) region(lstyle(none))) ///
	lcols(N_Studies_Str N_Cases_Str Range_Amount2) xlabel(.5 "0.5" 1 2) astext(70) graphregion(color(white))
```


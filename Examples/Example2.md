<a href ="https://www.mrcctu.ucl.ac.uk/"><img src="../logo_ukri-mrc-ctu_transparent-background.png" width="50%" /></a>

# Example 2
Fixed- and random-effects results displayed simultaneously, with predictive interval.

Data taken from Supplementary Figure S1 of Liu X et al. Int. J. Cancer 2020 doi: 10.1002/ijc.32744

<a href ="https://github.com/UCL/metan/blob/main/Examples"><img src="Example%202.png" width="50%" /></a>

```Stata
* Example generated by -dataex-. For more info, type help dataex
clear
input str26 Disease str23 StudyName str25 Intervention str11 Control int(IntN ControlN) float(HR_pos HRlci_pos HRuci_pos HR_neg HRlci_neg HRuci_neg)
"Non-small-cell lung cancer" "CheckMate 057, 2017"     "Nivolumab"                 "Docetaxel"   231 224 .43  .3  .63 1.01 .76 1.33
"Non-small-cell lung cancer" "CheckMate 017, 2017"     "Nivolumab"                 "Docetaxel"   117 108 .53 .31  .89   .7 .47 1.02
"Non-small-cell lung cancer" "POPLAR, 2016"            "Atezolizumab"              "Docetaxel"   144 143 .54 .33  .89  .84 .53 1.32
"Non-small-cell lung cancer" "OAK, 2016"               "Atezolizumab"              "Docetaxel"   607 608 .64 .49  .84  .85 .73    1
"Melanoma"                   "CheckMate 067, 2018 (a)" "Nivolumab"                 "Ipilimumab"  288 277 .65 .42  .99  .64  .5  .82
"Melanoma"                   "CheckMate 067, 2018 (b)" "Nivolumab plus ipilimumab" "Ipilimumab"  278 277 .56 .35   .9  .54 .42  .69
"Melanoma"                   "CheckMate 069, 2016"     "Nivolumab plus ipilimumab" "Ipilimumab"   35  83 .78 .26  2.4  .74 .38 1.47
"Melanoma"                   "CheckMate 066, 2018"     "Nivolumab"                 "Dacarbazine" 120 243 .16 .07  .38  .56 .37  .85
"Melanoma"                   "CheckMate 037, 2017"     "Nivolumab"                 "CTx"         201 195 .73 .49 1.09 1.15 .82 1.62
"Head and neck cancer"       "CheckMate 141, 2018"     "Nivolumab"                 "SOC"         172 103  .5  .3  .83  .81 .55 1.21
end

* Setup prior to meta-analysis:
gen double lnHR_pos = ln(HR_pos)
gen double lnHRlci_pos = ln(HRlci_pos)
gen double lnHRuci_pos = ln(HRuci_pos)

gen double lnHR_neg = ln(HR_neg)
gen double lnHRlci_neg = ln(HRlci_neg)
gen double lnHRuci_neg = ln(HRuci_neg)

gen IntString = Intervention + " (n=" + strofreal(IntN, "%3.0f") + ")"
label variable IntString `""Intervention " "(No. of patients analysed)""'
format %-1s IntString

gen ControlString = Control + " (n=" + strofreal(ControlN, "%3.0f") + ")"
label variable ControlString `""Control" "(No. of patients analysed)""'
format %-1s ControlString

* Perform two meta-analyses, for PD-L1 positive and negative separately; then append and re-sort:
preserve
    tempfile neg
    metan lnHR_neg lnHRlci_neg lnHRuci_neg, random study(StudyName) by(Disease) nosubgroup nograph clear
    save `neg'
restore
metan lnHR_pos lnHRlci_pos lnHRuci_pos, random study(StudyName) by(Disease) lcols(IntString ControlString) nosubgroup nograph clear
append using `neg', gen(PDL1)

isid _BY _USE _STUDY PDL1, sort miss

* A little more data manipulation prior to running forestplot:
replace PDL1 = PDL1 + 1
label define PDL1_ 1 "PD-L1 Positive" 2 "PD-L1 Negative"
label values PDL1 PDL1_

drop if inlist(_USE, 0, 6) & PDL1==1
replace PDL1 = . if inlist(_USE, 0, 6)

replace _LABELS = "{bf:Pooled estimate in PD-L1 positive at 5% cut-off}" if _USE==5 & PDL1==1
replace _LABELS = "{bf:Pooled estimate in PD-L1 negative at 5% cut-off}" if _USE==5 & PDL1==2

replace _LABELS = "" if _USE!=5 & PDL1==2
label variable _LABELS "Study name"
format %-1s _LABELS

* Create some blank space at the base of the plot, into which to place the legend
local oldN = _N
set obs `=`oldN'+2'
replace _USE = 6 if _n > `oldN'

* And finally, creation of the plot:
forestplot, hr nowt plotid(PDL1) dataid(PDL1) lcols(IntString ControlString) xlabel(.5 "0.5" 1 2) range(.0625 2.5) cirange(.08 2.5) ///
    astext(70) textsize(90) boxsca(80) favours(Favours intervention # Favours control, fp(3)) ///
    box1opts(mcolor(edkblue)) box2opts(mcolor(orange)) ciopts(lcolor(black) lw(thin)) ///
    diam1opts(lc(edkblue)) diam2opts(lc(orange)) oline1opts(lp(dash) lc(edkblue)) oline2opts(lp(dash) lc(orange)) graphregion(color(white)) ///
	legend(order(1 "PDL-1 positive" 3 "PDL-1 negative") size(1.216) region(lstyle(none)) position(7) ring(0)) spacing(1.4)
```

